#!ipxe
# Some menu defaults
dhcp
imgfree

set menu-timeout 30000
set submenu-timeout ${menu-timeout}
isset ${menu-default} || set menu-default clonezilla
# Figure out if client is 64-bit capable
cpuid --ext 29 && set arch x64 || set arch x86
cpuid --ext 29 && set archl amd64 || set archl i386

:start
menu iPXE Boot Menu
item --gap --            ------------------------- Recovery Tools ----------------------------------
item --key k clonezilla  Clonezilla Live
item --key s synomedia   Synology Recovery Media
item --key m macrium     Macrium Recovery Media
item --key r rescuezilla Rescuezilla
item --key p minitool    MiniTool Partition Wizard
item --key 1 sysresccd   System Rescue CD
item --key 2 strelec10   Sergei Strelec WinPE 10
item --key 3 strelec11   Sergei Strelec WinPE 11
item --key e easyuefi    EasyUEFI Enterprise
item --gap --            ------------------------- Netboot (UEFI) ----------------------------------
item --key n netboot     netboot.xyz
item --gap --            ---------------------------------------------------------------------------
item --key c config      Configure settings
item shell               Drop to iPXE shell
item reboot              Reboot computer
item
item --key x exit        Exit iPXE and continue BIOS boot
choose --default ${menu-default} --timeout ${menu-timeout} selected || goto cancel
set menu-timeout 0
goto ${selected}

:cancel
echo You cancelled the menu, dropping you to a shell

:shell
echo Type 'exit' to get the back to the menu
shell
set menu-timeout 0
set submenu-timeout 0
goto start

:failed
echo Booting failed, dropping to shell
goto shell

:reboot
reboot

:exit
exit

:config
config
goto start

:back
set submenu-timeout 0
clear submenu-default
goto start

############ MAIN MENU ITEMS ############
:sysresccd
set base-url http://192.168.1.1/sysresccd
kernel ${base-url}/vmlinuz initrd=intel_ucode.img,amd_ucode.img,sysresccd.img archisobasedir=/ ip=dhcp archiso_http_srv=${base-url}/ checksum setkmap=it
initrd ${base-url}/intel_ucode.img
initrd ${base-url}/amd_ucode.img
initrd ${base-url}/sysresccd.img
boot || goto failed

:macrium
set base-url http://192.168.1.1/macrium
kernel ${base-url}/wimboot
initrd ${base-url}/boot/BCD         BCD
initrd ${base-url}/boot/boot.sdi    boot.sdi
initrd ${base-url}/sources/boot.wim boot.wim
boot || goto failed

:synomedia
set base-url http://192.168.1.1/synomedia
kernel ${base-url}/wimboot
initrd ${base-url}/boot/BCD         BCD
initrd ${base-url}/boot/boot.sdi    boot.sdi
initrd ${base-url}/sources/boot.wim boot.wim
boot || goto failed

:minitool
set base-url http://192.168.1.1/minitool
kernel ${base-url}/wimboot
initrd ${base-url}/boot/bcd         BCD
initrd ${base-url}/boot/boot.sdi    boot.sdi
initrd ${base-url}/sources/boot.wim boot.wim
boot || goto failed

:strelec10
set base-url http://192.168.1.1/strelec
kernel ${base-url}/wimboot
initrd ${base-url}/bcd         BCD
initrd ${base-url}/boot.sdi    boot.sdi
initrd ${base-url}/strelec10x64Eng.wim    strelec10x64Eng.wim
boot || goto failed

:strelec11
set base-url http://192.168.1.1/strelec
kernel ${base-url}/wimboot
initrd ${base-url}/bcd         BCD
initrd ${base-url}/boot.sdi    boot.sdi
initrd ${base-url}/strelec11x64Eng.wim    strelec11x64Eng.wim
boot || goto failed

:easyuefi
set base-url http://192.168.1.1/easyuefi
kernel ${base-url}/wimboot
initrd ${base-url}/boot/bcd         BCD
initrd ${base-url}/boot/boot.sdi    boot.sdi
initrd ${base-url}/sources/boot.wim boot.wim
boot || goto failed

:clonezilla
set base-url http://192.168.1.1/clonezilla
set nas-ip 192.168.1.1
kernel ${base-url}/vmlinuz initrd=initrd.img boot=live username=user union=overlay config components quiet noswap edd=on nomodeset nodmraid keyboard-layouts=it locales=it_IT.UTF-8 fetch=${base-url}/filesystem.squashfs ocs_numlk=on ocs_daemonon="ssh" ocs_prerun="mount -t nfs ${nas-ip}:/volume2/backup /home/partimag"
initrd ${base-url}/initrd.img
boot || goto failed

:rescuezilla
set base-url http://192.168.1.1/rescuezilla
kernel ${base-url}/vmlinuz initrd=initrd.lz boot=casper quiet splash fastboot toram root=/dev/ram0 ramdisk_size=15000000 bootkbd=it console-setup/layoutcode=it ip=dhcp url=${base-url}/rescuezilla.iso
initrd ${base-url}/initrd.lz
boot || goto failed

:netboot
chain --autofree http://boot.netboot.xyz/ipxe/netboot.xyz.efi
